plugins {
    // Apply the groovy plugin to add support for Groovy
    id 'groovy'
    id 'maven-publish'
    id 'signing'
    id("com.github.johnrengelman.shadow") version "8.1.1"
}

repositories {
    maven { url 'https://jitpack.io' }
}

configurations {
    gormTestCompile.extendsFrom integrationTestCompile
    gormTestRuntime.extendsFrom integrationTestRuntime
    gormTestImplementation.extendsFrom integrationTestImplementation

    mdTestCompile.extendsFrom integrationTestCompile
    mdTestRuntime.extendsFrom integrationTestRuntime
    mdTestImplementation.extendsFrom integrationTestImplementation

    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
    integrationTestImplementation.extendsFrom testImplementation
}

sourceSets.main.java.srcDirs = []
sourceSets.main.groovy.srcDirs += ["src/main/java"]

sourceSets {
//    gormTest {
//        groovy.srcDir file('src/gormTest/groovy')
//        groovy.srcDir file('src/integrationTest/groovy/ai/stainless/micronaut/jupyter/kernel')
//        groovy.exclude { FileTreeElement elem ->
//            elem.file.absolutePath.contains('src/integrationTest/groovy') &&
//                    !elem.file.absolutePath.contains('KernelSpec.groovy')
//        }
//        resources.srcDir file('src/test/resources')
//        compileClasspath += sourceSets.main.output + configurations.testRuntime
//        runtimeClasspath += output + compileClasspath
//    }

    mdTest {
        groovy.srcDir file('src/mdTest/groovy')
        groovy.srcDir file('src/integrationTest/groovy/ai/stainless/micronaut/jupyter/kernel')
        groovy.exclude { FileTreeElement elem ->
            elem.file.absolutePath.contains('src/integrationTest/groovy') &&
                    !elem.file.absolutePath.contains('KernelSpec.groovy')
        }
        resources.srcDir file('src/test/resources')
        compileClasspath += sourceSets.main.output + configurations.testRuntime
        runtimeClasspath += output + compileClasspath
    }

    integrationTest {
        groovy.srcDir file('src/integrationTest/groovy')
        resources.srcDir file('src/test/resources')
        compileClasspath += sourceSets.main.output + configurations.testRuntime
        runtimeClasspath += output + compileClasspath
    }
}

dependencies {
    annotationProcessor platform("io.micronaut.platform:micronaut-platform:$micronautVersion")
    annotationProcessor "io.micronaut:micronaut-inject-java"
    implementation("io.micronaut.rxjava2:micronaut-rxjava2:1.2.0")
    implementation("io.micronaut.rxjava2:micronaut-rxjava2-http-client:1.2.0")

    implementation "org.apache.groovy:groovy-all:$groovyVersion"
    implementation "org.apache.groovy:groovy-xml:$groovyVersion"

    implementation platform("io.micronaut.platform:micronaut-platform:$micronautVersion")
    implementation "io.micronaut:micronaut-inject-groovy"
    implementation "io.micronaut:micronaut-messaging"
    implementation "io.micronaut:micronaut-inject"
    implementation "io.micronaut:micronaut-management"
    annotationProcessor("io.micronaut.security:micronaut-security-annotations")
    implementation "io.micronaut.security:micronaut-security"
    implementation "$beakerxGroupId:beakerx-kernel-base:$beakerxVersion"
    implementation "$beakerxGroupId:beakerx-kernel-groovy:$beakerxVersion"

    testAnnotationProcessor platform("io.micronaut.platform:micronaut-platform:$micronautVersion")
    testAnnotationProcessor "io.micronaut:micronaut-inject-java"

    testImplementation "org.apache.groovy:groovy-test:$groovyVersion"

    // Use the awesome Spock testing and specification framework
    testImplementation "org.spockframework:spock-core:$spockVersion"

    testImplementation "io.micronaut.test:micronaut-test-core:$micronautTestVersion"
    testImplementation "io.micronaut.test:micronaut-test-spock:$micronautTestVersion"
    testImplementation "io.micronaut.test:micronaut-test-junit5:$micronautTestVersion"
    testImplementation platform("io.micronaut.platform:micronaut-platform:$micronautVersion")
    testImplementation "io.micronaut:micronaut-inject-groovy"
    testImplementation "io.micronaut:micronaut-http-server-netty"
    testImplementation "io.micronaut:micronaut-http-client"
    testImplementation "io.micronaut:micronaut-jackson-databind"
    testImplementation "io.micronaut.security:micronaut-security"
    testImplementation "org.objenesis:objenesis:1.4"
    testImplementation "ch.qos.logback:logback-classic:1.2.3"

    integrationTestImplementation platform("org.testcontainers:testcontainers-bom:1.20.3")
    integrationTestImplementation "org.testcontainers:testcontainers"
    integrationTestImplementation "org.testcontainers:spock"
    integrationTestImplementation "org.testcontainers:junit-jupiter"
    
    // Copy essential test dependencies to integration test
    integrationTestImplementation "org.spockframework:spock-core:$spockVersion"
    integrationTestImplementation "io.micronaut.test:micronaut-test-spock:$micronautTestVersion"
    integrationTestImplementation "io.micronaut:micronaut-http-client"
    integrationTestImplementation "ch.qos.logback:logback-classic:1.2.3"
    
    // Add runtime dependencies needed for integration test application
    integrationTestImplementation "io.micronaut:micronaut-runtime:$micronautVersion"
    integrationTestImplementation "io.micronaut:micronaut-http-server-netty:$micronautVersion"
    integrationTestRuntimeOnly "io.micronaut:micronaut-http-server-netty:$micronautVersion"
    integrationTestImplementation "io.micronaut.groovy:micronaut-runtime-groovy"
    integrationTestImplementation "org.apache.groovy:groovy-all:$groovyVersion"
    
    // Add JUnit 5 dependencies needed for @Testcontainers
    integrationTestImplementation "org.junit.jupiter:junit-jupiter-api:5.8.2"
    integrationTestImplementation "org.junit.jupiter:junit-jupiter-engine:5.8.2"

    mdTestImplementation "io.micronaut.data:micronaut-data-processor:$micronautDataVersion"
    mdTestImplementation "io.micronaut.configuration:micronaut-jdbc-tomcat:1.2.3"
    mdTestImplementation "io.micronaut.data:micronaut-data-hibernate-jpa:$micronautDataVersion"
    mdTestImplementation project(':examples:md-service')
    mdTestRuntimeOnly "com.h2database:h2:1.4.200"

    testRuntimeOnly("org.yaml:snakeyaml")

//    gormTestImplementation "io.micronaut.configuration:micronaut-hibernate-gorm:1.1.1"
//    gormTestImplementation "io.micronaut.configuration:micronaut-jdbc-tomcat:1.2.3"
//    // For some reason, the gorm tests would fail with a NullPointerException
//    // if this dependency wasn't included
//    // There must be some sort of transitive dependency that the gorm tests
//    // need
//    gormTestImplementation "io.micronaut.data:micronaut-data-hibernate-jpa:$micronautDataVersion", {
//        exclude group: 'io.micronaut.configuration', module: 'micronaut-hibernate-jpa-spring'
//    }
//    gormTestRuntimeOnly "com.h2database:h2:1.4.200"
}

// get tests filter
def testsFilter = project.hasProperty("tests") ? tests : "*"
// count total tests
def testCount = 0

def createCheckTests = { isTest ->
    return {
        if (testCount != 0 || !gradle.startParameter.taskNames.contains(name)) {
            return
        }
        def testTasks = taskDependencies.getDependencies()
                .collect { [it, it.getDependsOn()] }
                .flatten()
                .findAll { it instanceof Test && it.didWork }
        if (isTest && didWork) {
            testTasks.add([name: name])
        }
        // if any of our dependent test tasks ran
        if (testTasks.size()) {
            logger.warn(
                    "Warning: No tests found for the given includes: [$testsFilter] among tasks: ${testTasks.collect { it.name }}"
            )
        }
    }
}

tasks.withType(Test) {
    useJUnitPlatform()
    testLogging {
        showStandardStreams = true
    }
    failFast = false
    afterSuite { desc, result ->
        if (!desc.parent) {
            testCount += result.testCount
        }
    }
    doLast createCheckTests(true)
}

//task gormTest(type: Test) {
//    testClassesDirs = sourceSets.gormTest.output.classesDirs
//    classpath = sourceSets.gormTest.runtimeClasspath
//}

task unitTest(type: Test) {
    testClassesDirs = sourceSets.test.output.classesDirs
    classpath = sourceSets.test.runtimeClasspath
}

shadowJar {
    dependsOn integrationTestClasses

    archiveBaseName = 'integration-test'
    archiveVersion = '0.1'
    archiveClassifier = 'all'

    // Include integration test classes and resources
    from sourceSets.integrationTest.output.classesDirs
    from sourceSets.integrationTest.output.resourcesDir

    // Include ONLY .class files from main (exclude main resources)
    from sourceSets.main.output.classesDirs

    // Use integration test runtime classpath
    configurations = [project.configurations.integrationTestRuntimeClasspath]

    mergeServiceFiles()

    manifest {
        attributes 'Main-Class': 'ai.stainless.micronaut.integration.Application'
        attributes 'Implementation-Title': 'Integration Test Application'
        attributes 'Implementation-Version': '0.1'
    }
}

task integrationTestJar {
    dependsOn shadowJar
}

task mdTestShadowJar(type: com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
    dependsOn mdTestClasses, processIntegrationTestResources

    archiveBaseName = 'md-test'
    archiveVersion = '0.1'
    archiveClassifier = 'all'

    // Enable zip64 to handle large archives
    zip64 = true

    // Include mdTest classes and resources
    from sourceSets.mdTest.output.classesDirs
    from sourceSets.mdTest.output.resourcesDir

    // Include integration test classes (KernelSpec is in integrationTest)
    from sourceSets.integrationTest.output.classesDirs
    from sourceSets.integrationTest.output.resourcesDir

    // Include ONLY .class files from main (exclude main resources)
    from sourceSets.main.output.classesDirs

    // Use mdTest runtime classpath
    configurations = [project.configurations.mdTestRuntimeClasspath]

    mergeServiceFiles()

    manifest {
        attributes 'Main-Class': 'ai.stainless.micronaut.integration.Application'
        attributes 'Implementation-Title': 'MD Test Application'
        attributes 'Implementation-Version': '0.1'
    }
}

task mdTestJar {
    dependsOn mdTestShadowJar
}

task mdTest(type: Test) {
    dependsOn mdTestJar
    
    testClassesDirs = sourceSets.mdTest.output.classesDirs
    classpath = sourceSets.mdTest.runtimeClasspath
}

task basicIntegrationTest(type: Test) {
    dependsOn integrationTestJar

    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath

    // Ensure test discovery works
    useJUnitPlatform()

    // Explicit test logging
    testLogging {
        events "passed", "skipped", "failed", "started"
        showStandardStreams = true
        exceptionFormat = "full"
    }

    // Force test execution
    outputs.upToDateWhen { false }
}

assemble.dependsOn integrationTestJar

task integrationTest() {
    dependsOn basicIntegrationTest
    // dependsOn gormTest
    dependsOn mdTest

    outputs.upToDateWhen { false }
    doLast createCheckTests()
}

test.dependsOn unitTest
test.dependsOn integrationTest
test.outputs.upToDateWhen { false }
test.doLast {
    createCheckTests()
}

task copyGroovydoc(type: Copy) {
    from "${project.buildDir}/docs/groovydoc"
    into "${rootProject.buildDir}/docs/gapi"
}

groovydoc {
    finalizedBy copyGroovydoc
}

publishing {
    publications {
        micronautJupyter(MavenPublication) {
            artifactId = 'micronaut-jupyter'
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = artifactId
                description = projectDesc
                url = projectUrl
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        name = "Joshua Carter"
                        email = "jcarter@stainless.ai"
                        organization = "Stainless.Ai"
                        organizationUrl = "https://stainless.ai"
                    }
                    developer {
                        name = "Dan Stieglitz"
                        email = "dstieglitz@stainless.ai"
                        organization = "Stainless.Ai"
                        organizationUrl = "https://stainless.ai"
                    }
                }
                scm {
                    connection = "scm:git:git://github.com/${githubSlug}.git"
                    developerConnection = "scm:git:ssh://github.com:${githubSlug}.git"
                    url = "http://github.com/${githubSlug}/tree/$githubBranch"
                }
            }
        }
    }
}

//signing {
//    sign publishing.publications.jupyterConfigurationLibrary
//}
